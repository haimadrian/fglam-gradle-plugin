buildscript {
  ext {
    artifactoryUrl = "https://artifactory.labs.quest.com/artifactory"

    repos = {
      maven {
        url "${artifactoryUrl}/libs-release"
        credentials {
          username "$artifactoryUser"
          password "$artifactoryAPIKey"
        }
      }
    }
  }
  repositories repos

  dependencies {
    classpath group: 'com.quest.foglight', name: 'foglight-gradle-plugin', version: foglightGradlePluginVersion
    classpath group: 'com.quest.foglight.ant', name: 'foglight-ant', version: foglightAntVersion
  }
}

plugins {
  id 'java'
  id 'idea'
  id 'eclipse'
  id 'groovy'
  id 'java-gradle-plugin'
  id 'maven-publish'
}

apply plugin: 'com.quest.foglight'

repositories repos

group = 'com.quest.foglight.fglam'
version = '1.2'

java {
  sourceCompatibility JavaVersion.VERSION_1_8
  targetCompatibility JavaVersion.VERSION_1_8
  withSourcesJar()
}

idea {
  module {
    downloadSources = true
  }
}

eclipse {
  classpath {
    downloadSources = true
  }
}

dependencies {
  compileOnly(gradleApi())

  // This is needed for com.quest.glue.tools.AgentCompiler, which is responsible for generating topology classes
  implementation("com.quest.glue:gluetools:$fglamDevkitVersion")
  implementation("com.quest.glue:glueapi:$fglamDevkitVersion")
  implementation("com.quest.glue:gluecore:$fglamDevkitVersion")
  implementation("com.quest.glue:gluecommon:$fglamDevkitVersion")
  implementation("com.quest.foglight.ant:foglight-ant:$foglightAntVersion")

  implementation("com.quest.foglight:foglight-gradle-plugin:$foglightGradlePluginVersion")

  implementation("com.quest.foglight.forge-common:quest-antlr:6.1.2") // Required by AgentCompiler (gluetools, quest-common)
  implementation("org.antlr:stringtemplate:4.0.2") // Required by com.quest.glue.tools.compiler.topology.TopologyTemplateGenerator
  implementation("com.sun.codemodel:codemodel:2.4") // Required by com.quest.glue.tools.compiler.CallbackCodeGenerator
  implementation("com.sun.xml.bind:jaxb-ri:2.3.3") // Required by com.quest.glue.tools.compiler.AgentDefinitionWrapper

  implementation(localGroovy())
  testImplementation(platform('org.junit:junit-bom:5.8.2'))
  testImplementation('org.junit.jupiter:junit-jupiter')
}

gradlePlugin {
  plugins {
    fglamBuildPlugin {
      id = 'com.quest.foglight.fglam'
      implementationClass = 'com.quest.foglight.fglam.gradle.FglAMBuildPlugin'
    }
  }
}

test {
  useJUnitPlatform()
  testLogging {
    events "passed", "skipped", "failed"
  }
}

artifactory {
  publish {
    defaults {
      publications {
        mavenJava(MavenPublication) {
          from components.groovy
        }
      }
      properties = ['dev.team': 'cartridges', 'build.name': 'fglam-gradle-plugin']
      publishBuildInfo = true
      publishPom = true
    }
  }
}

// Just leave me be... It is complaining on the hack with LogCategory.. WTH
checkstyle {
  sourceSets = []
}
pmd {
  sourceSets = []
}

tasks.named('publish').configure {
  group 'publishing'
  dependsOn tasks.build
  finalizedBy 'artifactoryPublish'
}

